/*
  • subject:
    把 filemaker 的 value list 轉成其他格式的資料
  • format:
    cf_listToAnother ( _valueList ; _to_sqlIn_csv_tsv_words_array ; _strOrNumber )
  • parameters:
    _valueList: filemaker value list
    _to_sqlIn_csv_tsv_words_array : 可以是
      sqlIn: 會產生 sql in 的條件字串, 但不用用小括號括起來
      csv: common separated value
      tsv: tab separated value
      words: space separated string ( default )
      array: json array
    _strOrNumber: 對 sqlIn 和 array 有區別是數字或者是字串, 如果不是 n 開頭都視為 str
  • returns:
    
  • dependencies: on other cfs, variables, table, fields, scheme or ...
    
  • version:
    v01
  • date:
    20240101
  • update infomation:
    
  • notes:
    
  • repo:
    /Volumes/MacHDD/MAMP/projects/cf/cf/cf_listToAnother.calc
  • initial:
    20240101
  • by:
    jeff liao
*/

Let (
  [
    /*
    _valueList = contact::idList;
    _to_sqlIn_csv_tsv_words_array = "w" ;
    _strOrNumber = "str"
    */
    ~strOrNumb = If ( Left (_strOrNumber ; 1 ) = "n" ; "num" ; "str" );
    ~to = Case ( 
      Left ( _to_sqlIn_csv_tsv_words_array ; 1 )  = "a" ; "array" ; 
      Left ( _to_sqlIn_csv_tsv_words_array ; 1 )  = "c" ; "csv" ; 
      Left ( _to_sqlIn_csv_tsv_words_array ; 1 )  = "t" ; "tsv" ; 
      Left ( _to_sqlIn_csv_tsv_words_array ; 1 )  = "s" ; "sqlIn" ; 
      "words" );
    ~toRep = Case ( 
      ~to = "array" and ~strOrNumb = "num" ; "," ;
      ~to = "array" ;  "\",\"" ;
      ~to = "csv" ;  "," ;
      ~to = "tsv" ;  Char(9) ;
      ~to = "sqlIn" and ~strOrNumb = "num" ; "," ;
      ~to = "sqlIn"; "','" ;
      " " );
    ~prefix = Case ( 
      ~to = "array" and ~strOrNumb = "num" ; "[" ; 
      ~to = "array" ; "[\"" ; 
      ~to = "sqlIn" and ~strOrNumb = "num" ; "" ;
      ~to = "sqlIn" ; "'" ;
      "" );
    ~trail = Case (
      ~to = "array" and ~strOrNumb = "num" ; "]" ; 
      ~to = "array" ; "\"]" ; 
      ~to = "sqlIn" and ~strOrNumb = "num" ; "" ;
      ~to = "sqlIn" ; "'" ;
      "" );
    ~vl = If ( Right ( _valueList ; 1 ) = ¶ ; Left ( _valueList ; Length( _valueList ) - 1 ); _valueList );
    endInitial = True
  ];
  ~prefix & Substitute ( ~vl ; ¶ ; ~toRep ) & ~trail
)